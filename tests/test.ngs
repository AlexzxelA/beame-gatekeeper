#!/usr/bin/env ngs

# NGS language - https://github.com/ilyash/ngs/

# ---------- Environment sanity checks ----------

assert('BEAME_INTERNAL_AUTH_SERVER_FQDN' in ENV, 'Environment variable BEAME_INTERNAL_AUTH_SERVER_FQDN must be set')

if 'BEAME_DIR' in ENV {
	throw Error("BEAME_DIR environment variable must be unset for tests")
}

HOME = ENV.HOME

DIRS = [
	HOME / '.beame'
	HOME / '.beame_server'
	HOME / '.beame_data'
]

BEAME_INSTA_SERVER_BIN = HOME / 'beame-insta-server/main.js'
BEAME_SDK_BIN = HOME / 'beame-sdk/src/cli/beame.js'
BEAME_DEV_AUTH_FQDN = 'n6ge8i9q4b4b5vb6.h40d7vrwir2oxlnn.v1.d.beameio.net'

CONFIG_DIR = "${HOME}/.beame_server/config"
APP_CONFIG_FILE = CONFIG_DIR / 'app_config.json'

CREDS_DIR = "${HOME}/.beame_server/creds"
CREDS_FILE = CREDS_DIR / 'creds.json'
AUTH_SERVERS_FILE = CREDS_DIR / 'auth_servers.json'

TEST_EXISTING_DIR = ENV.get('TEST_EXISTING_DIR', false)

TOKEN_TYPES=%[admin config]

if not(TEST_EXISTING_DIR) {
	each(DIRS, F(dir) {
		if Path(dir) {
			throw Error("Directory ($dir) exists. Will not run tests.")
		}
	})
}

# ---------- Environment setup ----------

{
  ENV.SNI_SERVER_PORT = '0'
  ENV.BEAME_LOG_LEVEL = 'DEBUG'
}

# ---------- Utilities ----------

F base64encode(s:Str) {
	fname = "/tmp/base64.${c_getpid()}"
	$(echo $s >$fname)
	ret = `base64 -w0 <$fname`
	$(rm $fname)
	log("base64 encode done")
	ret
}

F base64decode(s:Str) {
	fname = "/tmp/base64decode.${c_getpid()}"
	$(echo $s >$fname)
	ret = `base64 -d <$fname`
	$(rm $fname)
	log("base64 decode done")
	ret
}

F fqdn_dir(fqdn) "$HOME/.beame/v2/$fqdn"

# ---------- Tests library ----------
doc Copies Beame test requests signing credential into store for L0 entity creation
F copy_to_store() {
	$(cp -a "$HOME/$BEAME_DEV_AUTH_FQDN" ${fqdn_dir(BEAME_DEV_AUTH_FQDN)})
	'Copied'
}

doc Creates token needed for signing request for level 0 entity
F create_token() {
	global token = `$BEAME_SDK_BIN token create --fqdn $BEAME_DEV_AUTH_FQDN --data NONE`
	assert(token is Str, "Token expected to be a string")
	token
}

doc Initializes beame-insta-server
F create() {
    t = time()
    # src 3 - RegistrationSource.InstaServerSDK
    beame_insta_server_token = {
        'authToken': token.base64decode().parse()
        'authSrvFqdn': ENV.BEAME_INTERNAL_AUTH_SERVER_FQDN
        'name': "insta-server-test-L0-$t"
        'email': "insta-server-L0-${t}@example.com"
        'src': 3
    }

    txt = `$BEAME_INSTA_SERVER_BIN creds getCreds --regToken ${beame_insta_server_token.encode_json().base64encode()}`
    # cert_created_line = txt.lines().filter(X ~ Pfx('Certificate created'))[0]
    cert_created_line = txt.lines().filter(Pfx('Certificate created'))[0]
    global fqdn = cert_created_line.split(' ')[-1]
    TestMessage("Entity $fqdn created")
}

doc Removes Beame test requests signing credential from store
F remove_from_store() {
	$(rm -r ${fqdn_dir(BEAME_DEV_AUTH_FQDN)})
	TestMessage('Removed')
}

app_tokens = {}

F get_app_proxying_token(kind:Str) {
    test("Get $kind app token") with {
        t = `$BEAME_INSTA_SERVER_BIN $kind`
        links = t.lines().filter(/^https/)
        links.len() != 1 throws TestFail("Link not found or more than one link found").set('out', t)
        link = links[0]
        link.len() < 100 throws TestFail("Link is too short").set('link', link)
        app_tokens[kind] = link
        TestMessage("Got $kind token: ${app_tokens[kind]}")
    }
}

# ---------- Tests ----------

# TEMP WORKAROUND broken assert_output_has
F assert_output_has(cp:CommandsPipeline, s:Str) {
	assert(s in Str(cp))
}

if not(TEST_EXISTING_DIR) {
	test("Run without certificates") with {
		$(ok:1 $BEAME_INSTA_SERVER_BIN creds list).assert_output_has('you have no certificates')
		"OK"
	}
	test("Copy $BEAME_DEV_AUTH_FQDN into store") with copy_to_store
	test("Create token for test L0 entity") with create_token
	test("Initialize insta server") with create
	test("Remove $BEAME_DEV_AUTH_FQDN from store") with remove_from_store

	db_file = null
	test("Validate app config file at $APP_CONFIG_FILE") with {
		data = fetch(APP_CONFIG_FILE)

		must_have = {
			'db_provider': 'NeDB'
		}

		data.assert_hash_keys_values(must_have, "App config file $APP_CONFIG_FILE")
		data.assert_hash_keys(%[AppId ServiceName], "App config file $APP_CONFIG_FILE")

		global db_files = (%[users services registrations gk_logins sessions] + ".db").map(data.nedb_storage_root / X)
	}

	for db_file in db_files {
		test("Presence of database at $db_file") with {
			db_file.assert_path_exists("DB file")
		}
	}

}

gw_fqdn = null
test("Validating creds file $CREDS_FILE") with {
    data = read(CREDS_FILE)
    expected_fqdn_roles = %[ZeroLevel GatewayServer BeameAuthorizationServer MatchingServer AdminServer]
    ks = expected_fqdn_roles
    data.assert_hash_keys(ks + ['Users'], "Top level of $CREDS_FILE")
    expected_fqdn_roles.each(F(role) {
        data[role].assert_hash_keys(%[fqdn server], "Section $role in $CREDS_FILE")
        data[role].fqdn.assert_min_len(10, "Minimal FQDN length of role $role in $CREDS_FILE")
    })
    global gw_fqdn = data.GatewayServer.fqdn
}

test("Validating authorization servers list file $AUTH_SERVERS_FILE") with {
    data = read(AUTH_SERVERS_FILE)
    data.assert_hash_keys(['Servers'], "Section 'Servers' in $AUTH_SERVERS_FILE")
    data.Servers.assert_array("'Servers' section in $AUTH_SERVERS_FILE")
    data.Servers.assert_min_len(1, "Trusted auth servers")
}

# get_app_proxying_token('config')
each(TOKEN_TYPES, get_app_proxying_token)


test("Resolving gateway FQDN $gw_fqdn") with {
    assert_resolvable(gw_fqdn, "Resolve gateway FQDN")
}

echo("*** Starting insta server ***")
p = $(node --trace-deprecation --trace-warnings $BEAME_INSTA_SERVER_BIN server &)

retry(times=18, sleep=5)
    body => { $(fuser 65510/tcp) }
    progress_cb => { echo("Waiting for insta server to be up - port 65510: $A/$B") }

echo("*** Waiting for insta server to be up - another 5 seconds ***")
sleep 5

test("Front page at https://$gw_fqdn") with {
    page = `curl -s "https://$gw_fqdn"`
    page.assert_has('main-wrapper', 'Front page content')
}

COOKE_FILE='/dev/shm/test_cookies'

test("Admin token") with {
    admin_page = `curl -s -b $COOKE_FILE -c $COOKE_FILE -L ${app_tokens.admin}`
    admin_page.assert_has('function initAdminRouters', 'Admin page content')
}

# app is not ready
test("Config token") with {
    admin_page = `curl -s -b $COOKE_FILE -c $COOKE_FILE -L ${app_tokens.config}`
    admin_page.assert_has('Probably invalid app_id', 'Config page content')
}

push(exit_hook, {
    echo("Killing gatekeeper")
    p.kill()
})
